plugins {
    id 'java-library'
    id("io.spring.dependency-management") apply false
    id("org.springframework.boot") apply false
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    //编译时使用的编码为 UTF-8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    //控制台能够正确输出中文字符
    tasks.withType(JavaExec) {
        jvmArgs = ['-Dfile.encoding=UTF-8', '-Dsun.stdout.encoding=UTF-8', '-Dsun.stderr.encoding=UTF-8']
    }

}

subprojects { subproject ->

    apply {
        plugin("java-library")
        plugin("io.spring.dependency-management")
    }

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    dependencies {
        implementation platform(libs.spring.boot)
    }

    //全局剔除依赖
    configurations.all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
        exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc'
        exclude group: 'com.mchange', module: 'c3p0'
    }
}